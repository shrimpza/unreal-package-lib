plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

group = 'net.shrimpworks'
version = "1.4"

sourceCompatibility = '11'
targetCompatibility = '11'

if (System.getenv().containsKey("TRAVIS_BUILD_NUMBER")) {
    version = "${version}.${System.env.TRAVIS_BUILD_NUMBER}"
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHub"
            url = "https://maven.pkg.github.com/${System.env.GH_USER}/${project.name}"
            authentication {
                basic(BasicAuthentication)
            }
            credentials(PasswordCredentials) {
                username = "${System.env.GH_USER}"
                password = "${System.env.GH_TOKEN}"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.4.2')
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

test {
    useJUnitPlatform()
}


jacocoTestReport {
    reports {
        xml.enabled true
    }
}

