plugins {
	id 'java-library'
	id 'maven-publish'
	id 'jacoco'
	id 'org.gradlex.extra-java-module-info' version '1.13.1'
}

group = 'net.shrimpworks'
version = "1.14"
if (System.getenv().containsKey("BUILD_NUMBER")) {
	version += ".${System.env.BUILD_NUMBER}"
} else version += ".DEV"

compileJava {
	options.release = 21
}


repositories {
	mavenCentral()
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://code.shrimpworks.za.net/artefacts/'
			authentication {
				basic(BasicAuthentication)
			}
			credentials(PasswordCredentials) {
				username = "${System.env.ARTEFACTS_USER}"
				password = "${System.env.ARTEFACTS_PASSWORD}"
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
}

dependencies {
  implementation('org.anarres.lzo:lzo-core:1.0.6') {
		exclude group: 'com.google.code.findbugs', module: 'annotations'
		exclude group: 'commons-logging', module: 'commons-logging'
	}

  testImplementation('org.junit.jupiter:junit-jupiter:5.14.1')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

extraJavaModuleInfo {
	// failOnMissingModuleInfo.set(false)
	module("org.anarres.lzo:lzo-core", "lzo.core") {
		exports("org.anarres.lzo")

		requires("java.base")
	}
}

jar {
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': project.version
		)
	}
}

test {
	useJUnitPlatform()
}


jacocoTestReport {
	reports {
		xml.required = true
	}
}
